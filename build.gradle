plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.4'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.asciidoctor.jvm.convert' version '3.3.2'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

java {
    toolchain { languageVersion = JavaLanguageVersion.of(17) }
}

repositories {
    mavenCentral()
}

configurations {
    asciidoctorExt
}

ext {
    // REST Docs 스니펫 위치
    snippetsDir = file("${buildDir}/generated-snippets")
}
snippetsDir.mkdirs()

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'com.h2database:h2'
    annotationProcessor 'org.projectlombok:lombok'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'io.rest-assured:rest-assured:5.4.0'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'
    implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
    annotationProcessor "com.querydsl:querydsl-apt:${dependencyManagement.importedProperties['querydsl.version']}:jakarta"
    annotationProcessor 'jakarta.annotation:jakarta.annotation-api'
    annotationProcessor 'jakarta.persistence:jakarta.persistence-api'
    implementation 'org.springframework.cloud:spring-cloud-starter-aws:2.2.6.RELEASE'
    implementation 'org.springframework.boot:spring-boot-starter-websocket'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
}

// 1) 테스트에서 스니펫 생성
test {
    useJUnitPlatform()
    outputs.dir snippetsDir
    systemProperty 'org.springframework.restdocs.outputDir', snippetsDir
}

// 2) index.adoc 자동 생성
tasks.register("generateApiIndex") {
    group = "documentation"
    description = "src/docs/asciidoc 폴더의 .adoc 목록으로 index.adoc 생성"
    doLast {
        def srcDir = file("src/docs/asciidoc")
        srcDir.mkdirs()
        def out = new File(srcDir, "index.adoc")
        def sb = new StringBuilder()
                .append("= API 문서 인덱스\n")
                .append(":doctype: book\n")
                .append(":toc: left\n")
                .append(":toclevels: 2\n\n")
                .append("== 문서 목록\n\n")

        fileTree(dir: srcDir, include: '*.adoc')
                .filter { it.name != 'index.adoc' }
                .sort { a, b -> a.name <=> b.name }
                .each { f ->
                    def name = f.name.replace('.adoc', '')
                    sb.append("* xref:${name}.html[${name} API 문서]\n")
                }

        sb.append("""

== 사용 방법

http://localhost:8080/docs/index.html

== 문서 위치

JAR 내부 classpath:/static/docs/ 에 포함됩니다.
""".stripIndent())

        out.text = sb.toString()
        println "▶ index.adoc 생성 완료: ${out.path}"
    }
}

// 3) Asciidoctor 설정: .adoc → build/docs/html5/*.html
asciidoctor {
    dependsOn test, generateApiIndex
    sourceDir = file("src/docs/asciidoc")
    configurations 'asciidoctorExt'
    baseDirFollowsSourceFile()

    inputs.dir sourceDir
    inputs.files fileTree(dir: sourceDir, include: '**/*.adoc')
    inputs.dir snippetsDir

    // application.properties에서 참조하는 최종 폴더 위치
    outputDir = file("$buildDir/docs/html5/docs")

    attributes(
            'doctype': 'book',
            'toc': 'left',
            'toclevels': '2',
            'source-highlighter': 'highlightjs',
            'snippets': "${buildDir}/generated-snippets"
    )
}

//// (IDE/bootRun을 위해 processResources에도 복사하고 싶으면)
//tasks.named("processResources") {
//    dependsOn asciidoctor
//    from("$buildDir/docs/html5/docs") {
//        into "static/docs"
//    }
//}

// 5) bootJar 에 복사 (배포용)
tasks.named("bootJar") {
    dependsOn asciidoctor
    from("$buildDir/docs/html5/docs") {
        into 'static/docs'
    }
}

// 6) assemble 빌드에 포함
tasks.named("build") {
    dependsOn bootJar
}
